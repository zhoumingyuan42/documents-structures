# XQuery

## Introduction

`XQuery` ou `XML Query Language` est d√©fini par le [W3C](https://www.w3.org/XML/Query/). C'est un langage de requ√™tes pour exploiter, extraire de l'information des documents XMLs. On peut dire que c'est un peu le `SQL` du XML.

C'est une extension de `XPath`, ainsi la syntaxe, les concepts et les fonctions sont reprises.

`XQuery` est li√© √† `XSLT`, on peut s'en servir pour transformer des documents √©galement. N√©anmoins, il ne s'agit **pas** d'une syntaxe XML.

C'est un langage de programmation dit `fonctionnel`. Chaque expression √©value un r√©sultat, le r√©sultat est toujours le m√™me donc il n'y a pas d'effet de bord. Les variables sont **immuables*, il n'est pas possible de modifier la valeur une fois assign√©e.

Il est √©galement adapt√© pour mettre √† jour des documents XMLs.

## Syntaxe

### Pr√©ambule

On peut √©valuer directement le r√©sultat d'une expression, ainsi `1 + 1`  donne bien `2`.

On peut construire des √©l√©ments en √©crivant le XML, l'expression `XQuery` sera alors mise entre accolades `{}`. Dans l'exemple ci-dessous, on construit les √©l√©ments `resultats` et `resultat` manuellement. Ensuite, on assigne en valeur le retour de l'expression `XQuery` √©valu√© par les accolades.

``` xquery
<resultats>
    <resultat>{1 + 1}</resultat>
    <resultat somme="{1 + 1}"/>
</resultats>

<resultats>
    <resultat>2</resultat>
    <resultat somme="2"/>
</resultats>
```

Les fonctions de la librairie standard peuvent √™tre invoqu√©es. Ne pas oublier les accolades.

``` xquery
<majuscule>{upper-case('hello world')}</majuscule>

<majuscule>HELLO WORLD</majuscule>
```

Parfois, on veut √©valuer un nom d'√©l√©ment dynamiquement. Il faut utiliser la construction `element {}{}`.

``` xquery
element {
    if((year-from-date(current-date()) mod 4) eq 0)
    then 'bisextile'
    else 'normal'
}
{current-date()}

<bisextile>2020-12-02+01:00</bisextile>
```

### Les commentaires

Un commentaire s'√©crit en entourant son contenu avec `(: :)`. Exemple :

``` xquery
(: un commentaire sur une ligne :)

(: Un commentaire
sur plusieurs
lignes :)
```

### Les variables

Une variable est assign√©e avec la construction `let $nomVar :=` o√π `$nomVar` d√©signe le nom attribu√© (ne pas oublier le `$`). Attention, on utilise l'op√©rateur `:=`.

``` xquery
let $n := 1
let $s := "Hello"
let $f := upper-case('hello')
```

### Les s√©quences

Une s√©quence se d√©clare comme pour `XPath`, en parenth√©sant son contenu `( ... )`.

``` xquery
('a', 'b')
('a', 1)
('a', 1, <hello/>)
```

### Concat√©nation

Il est possible de concat√©ner des cha√Ænes de caract√®res avec l'op√©rateur `||`.

```
'Hello ' ||¬†'world'     'Hello world'
1 ||¬†2                  '12' (type string m√™me si ce sont des nombres)
```

On peut aussi utiliser la fonction `concat()`.

### Application d'une fonction

L'op√©rateur `!` applique une fonction sur une valeur.

``` xquery
('a', 'b') ! upper-case(.)     'A', 'B'
```

### Cha√Æner des fonctions

Plut√¥t que d'ench√¢sser le contenu des fonctions entre parenth√®ses √† chaque fois, il est possible de les cha√Æner. Pour cela, on utilise l'op√©rateur `=>`.

```xquery
tokenize('Hello World', ' ') => reverse() => string-join(' ')   'World Hello'
```

Ici, on n'indique pas l'argument d'entr√©e de la fonction. Il est implicitement r√©cup√©r√© de la fonction pr√©c√©dente.

### üå∏ FLOWR

La construction la plus importante en `XQuery` est appel√©e `FLOWR` pour `FOR, LET, ORDER, WHERE, RETURN`.

Celle-ci permet d'it√©rer sur une s√©quence et de manipuler les √©l√©ments (`for`). On peut d√©clarer des variables √† l'int√©rieur (`let`), les trier (`order`), appliquer une condition (`where`) et renvoyer le r√©sultat (`return`).

``` xquery
for $x in (1 to 5)        (: It√©ration √† travers une s√©quence :)
    let $mul := $x * 2    (: Assignation d'une variable :)
    where $x > 2          (: Filtre sur une condition :)
    order by $x mod 3     (: Ordonne les √©l√©ments :)
return $x                 (: Retourne le r√©sultat :)

(: R√©sultat : 3 5 4 :)
```

Les clauses `order` et `where` sont optionnelles.

``` xquery
for $auteur in ('Marcel Arland', 'Jean Paulhan', 'Francis Ponge')
return string-length($auteur)
```

``` xquery
let $words := tokenize('Marcel Arland √©tait √† la NRF.', ' ')
return $words
```

On peut utiliser un `XPath` comme point d'entr√©e.

``` xquery
let $auteurs :=
    <auteurs>
        <auteur prenom="Marcel">Arland</auteur>
        <auteur prenom="Jean">Paulhan</auteur>
        <auteur prenom="Francis">Ponge</auteur>
    </auteurs>
    
for $auteur in $auteurs/auteur
    let $nom := $auteur/text()
    let $prenom := $auteur/@prenom
return $nom ||', '||$prenom

(: Arland, Marcel Paulhan, Jean Ponge, Francis :)
```

#### `group by`¬†

On peut grouper selon une condition.

``` xquery
let $auteurs :=
    <auteurs>
        <auteur sexe='m' prenom="Marcel">Arland</auteur>
        <auteur sexe='f' prenom="Marguerite">Yourcenar</auteur>
        <auteur sexe='m' prenom="Jean">Paulhan</auteur>
        <auteur sexe='m' prenom="Francis">Ponge</auteur>
        <auteur sexe='f' prenom="Pauline">R√©age</auteur>
    </auteurs>

for $auteur in $auteurs/auteur
    let $nom := $auteur/text()
    let $sexe:= $auteur/@sexe
    group by $sexe
return $nom

(: Yourcenar R√©age Arland Paulhan Ponge :)
```

Il est possible d'√©num√©rer les r√©sultats avec `count`.

``` xquery
let $auteurs :=
    <auteurs>
        <auteur sexe='m' prenom="Marcel">Arland</auteur>
        <auteur sexe='f' prenom="Marguerite">Yourcenar</auteur>
        <auteur sexe='m' prenom="Jean">Paulhan</auteur>
        <auteur sexe='m' prenom="Francis">Ponge</auteur>
        <auteur sexe='f' prenom="Pauline">R√©age</auteur>
    </auteurs>

for $auteur in $auteurs/auteur
    let $nom := $auteur/text()
    order by $nom
    count $num
return $num||'.'||$nom

(: 1.Arland 2.Paulhan 3.Ponge 4.R√©age 5.Yourcenar :)
```

### Les espaces de noms

Pour effectuer ses requ√™tes sur un document comportant un espace de noms, il faut le d√©clarer au pr√©alable avec `declare namespace prefixe = "uri";`

``` xquery
declare namespace t = "http://www.tei-c.org/ns/1.0";
 
let $ns := t:TEI/name(child::*[1])
return $ns

(: teiHeader :)
```

### Query dans un document

Pour pr√©ciser dans quel document on souhaite effectuer la requ√™te, on utilise l'instruction `doc(file:///path)`.

``` xquery
declare namespace t = "http://www.tei-c.org/ns/1.0";
 
let $rom := doc('file:///Rom.xml')

for $editor in $rom//t:editor
return $editor/text()
```

ou

``` xquery
declare namespace t = "http://www.tei-c.org/ns/1.0";

for $editor in doc('file:///Rom.xml')//t:editor
return $editor/text()
```

### `if then else`

Les conditions sont exprim√©es avec `if then else`. Attention, le `else` est toujours obligatoire ! Si on ne veut rien retourner, alors il faut renvoyer une s√©quence vide `()`.

``` xquery
for $i in (1 to 10)
return if ($i mod 2 = 0) then $i else 'x'

(: x 2 x 4 x 6 x 8 x 10 :)
```

### Les fonctions

On peut d√©finir ses propres fonctions, pour cela il faut indiquer un espace de noms qui est propre `declare namespace mon-prefixe = "mon-uri";`. La d√©claration se termine toujours par un point-virgule `;`.

Ensuite, on d√©clare la fonction en utilisant ce pr√©fixe.

Les arguments et la valeur de retour doivent √™tre typ√©s ([https://www.w3.org/TR/xpath-datamodel-31/#types-hierarchy](https://www.w3.org/TR/xpath-datamodel-31/#types-hierarchy)). 

Le corps de la fonction est entour√© par des accolades, la valeur retourn√©e sera l'√©valuation de cette partie.

``` xquery
declare namespace local = "documents-structures-fonctions";

declare function local:bonjour($nom as xs:string) as xs:string {
    "Bonjour, " || $nom
};

local:bonjour('Marcel')

(: Bonjour Marcel :)
```

Parfois, il est n√©cessaire d'employer le `return` explicitement en retour.

``` xquery
declare namespace local = "documents-structures-fonctions";

declare function local:bonjour($nom as xs:string) as xs:string {
    let $greeting := "Bonjour, " || $nom
    return $greeting
};

local:bonjour('Marcel')

(: Bonjour Marcel :)
```

### Importer ses fonctions

Une fois les fonctions cr√©√©es, il est possible de se faire sa propre biblioth√®que. Pour cela, on peut d√©clarer un module avec une extension `.xqm`. Ce module contiendra toutes nos fonctions.

Il a une instruction particuli√®re √† d√©clarer au d√©but du fichier : `declare module namespace prefixe="mon-uri"`.

Ensuite, on peut d√©finir nos fonctions.

Le module s'importe avec l'instruction `import module namespace prefixe="mon-uri" at "mon-fichier.xqm"`.

Les fonctions sont utilisables avec le pr√©fixe d√©fini.

### Construire des documents

Nous avons vu au d√©but que l'on pouvait √©valuer des expressions avec les accolades. On peut se servir du m√™me m√©canisme pour produire d'autres documents.

``` xquery
<ul> {
let $auteurs :=
<auteurs>
<auteur sexe='m' prenom="Marcel">Arland</auteur>
<auteur sexe='f' prenom="Marguerite">Yourcenar</auteur>
<auteur sexe='m' prenom="Jean">Paulhan</auteur>
<auteur sexe='m' prenom="Francis">Ponge</auteur>
<auteur sexe='f' prenom="Pauline">R√©age</auteur>
</auteurs>

for $auteur in $auteurs/auteur
return <li>{$auteur/text()}</li>
}
</ul>
```

Produis le fragment HTML suivant

``` xquery
<ul>
   <li>Arland</li>
   <li>Yourcenar</li>
   <li>Paulhan</li>
   <li>Ponge</li>
   <li>R√©age</li>
</ul>
```

### Ench√¢sser les `for`

Les boucles ont la possibilit√© d'√™tre ench√¢ss√©es.

``` xquery
for $i in (1 to 5)
    for $y in (2, 3)
return $i * $y

(: 2 3 4 6 6 9 8 12 10 15 :)
```

On peut √©galement omettre le second `for`, dans ce cas on s√©pare les boucles par une virgule

``` xquery
for $i in (1 to 5), $y in (2, 3)
return $i * $y

(: 2 3 4 6 6 9 8 12 10 15 :)
```

## Exercice

### Partie 1

Cet exercice se base sur le [louch√©bem](https://fr.wikipedia.org/wiki/Louch%C3%A9bem) (qu'on va simplifier).

Le principe consiste √† reporter la premi√®re consonne du mot √† la fin de celui-ci en ajoutant un suffixe `em`. √Ä la place de cette consonne, on ajoute un `L`. Si le mot d√©bute par une voyelle, on ajoute √©galement un  `L` et le suffixe `-bem`. Exemple :

- `argot` : `largotbem`
- `consonne` : `lonsonnecem`
- `boucher` : `loucherbem`

1. √âcrire une fonction qui prend en entr√©e un mot et renvoie celui-ci avec les r√®gles √©nonc√©es au dessus. Vous pouvez voir avec [substring](http://www.xqueryfunctions.com/xq/fn_substring.html).

2. √âcrire une seconde fonction qui cette fois prend en entr√©e une phrase et modifie *tous* les mots de celle-ci.

### Partie 2

En repartant du document `juicers.xml`

1. √âcrire une expression pour afficher tous les `@id`¬†des juicers.
2. Enrichir celle-ci en triant par le prix (donc les ids seront class√©s selon le prix des juicers)
3. Toujours √† partir de cette expression, n'afficher que les juicers ayant dans leur nom la chaine `Juicer`.

### Partie 3

Nous allons cr√©er un module pour compter le nombre de mots dans un texte.

1. √âcrire une fonction qui normalise tous les mots d'un texte (mettre en minuscule, retirer la ponctuation) et renvoie une s√©quence de ceux-ci.
2. √âcrire une fonction qui prend en entr√©e une liste de mots et renvoie leur fr√©quence (voir [distinct-values](http://www.xqueryfunctions.com/xq/fn_distinct-values.html) et [count](http://www.xqueryfunctions.com/xq/fn_count.html)). Le r√©sultat attendu est le XML suivant :

```xml
<dictionnaire>
  <mot frequence="5">Le</mot>
  <mot frequence ="1">petit</mot>
  ...
  <mot frequence ="1">chat</mot>
  <mot frequence ="4">dort</mot>
</dictionnaire>
```
